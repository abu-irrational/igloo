#-------------------------------------------------------------------------
# TITLE:
#    igloo.test
#
# PROJECT:
#    igloo: Your project description
#
# DESCRIPTION:
#    igloo: Test Suite
#-------------------------------------------------------------------------

#-------------------------------------------------------------------------
# Load the tcltest package

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.3
    eval ::tcltest::configure $argv
}

namespace import ::tcltest::test

#-------------------------------------------------------------------------
# Load the package to be tested

source ../../lib/igloo/pkgModules.tcl

#-------------------------------------------------------------------------
# Setup and Cleanup

proc cleanup {} {
    catch {::dog destroy}
    catch {::animal destroy}
}

#-------------------------------------------------------------------------
# igloo::define method

test method-1.1 {method declares method} -body {
    igloo::class create ::dog
    igloo::define ::dog method bark {} { return "Woof" }
    ::dog create fido 
    fido bark
} -cleanup {
    cleanup
} -result {Woof}


#-------------------------------------------------------------------------
# var

test variable-1.1 {variable declares variable} -body {
    igloo::class create ::dog {
        variable name
        constructor {} { set name "Fido" }
        method name {} { return $name    }
    }
    ::dog create fido 
    fido name
} -cleanup {
    cleanup
} -result {Fido}

test variable-1.2 {variable initializes variable with empty string} -body {
    igloo::class create ::dog {
        variable name
        method name {} { return "*$name*" }
    }
    ::dog create fido 
    fido name
} -cleanup {
    cleanup
} -result {**}

test variable-1.3 {variable initializes variable with value} -body {
    igloo::class create ::dog {
        variable name "Fido"
        method name {} { return "*$name*" }
    }
    ::dog create fido
    fido name
} -cleanup {
    cleanup
} -result {*Fido*}

test variable-1.4 {variable initializes variable with array value} -body {
    igloo::class create ::dog {
        variable data -array {
            name "Fido"
            color "brown"
        }
        method data {} { return [array get data] }
    }
    ::dog create fido 
    fido data
} -cleanup {
    cleanup
} -result {name Fido color brown}

test variable-2.1 {parent class vars are initialized} -body {
    igloo::class create ::animal {
        variable name "Joe"

        method name {} { return $name }
    }

    igloo::class create ::dog {
        superclass ::animal
        variable breed "mutt" 

        method breed {} { return $breed }
    }
    ::dog create fido 
    list [fido name] [fido breed]
} -cleanup {
    cleanup
} -result {Joe mutt}

test variable-2.2 {child class vars override superclass vars} -body {
    igloo::class create ::animal {
        variable name "Joe"

        method name {} { return $name }
    }

    igloo::class create ::dog {
        superclass ::animal
        variable name "Fido"
    }
    ::dog create fido 
    fido name
} -cleanup {
    cleanup
} -result {Fido}


#-------------------------------------------------------------------------
# Cleanup

::tcltest::cleanupTests
